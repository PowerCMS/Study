※ 本項では静的出力の場合を想定しています

# ファンクションタグ

## 概要

ファンクションタグでは、ハンドラの処理の戻り値がそのまま出力されます。

## 例

下記の例では、記事の test_text1 カラムの値を出力する「MTEntryTestText1」タグを作成することを想定しています。
プラグイン名は「EntryTagSample」とします。

### config.yaml の指定

```
# plugins/EntryTagSample/config.yaml
tags:
  function:
    EntryTestText1: $entrytagsample::EntryTagSample::Tags::_hdlr_entry_test_text1
```

### ハンドラの指定

```
# plugins/EntryTagSample/lib/EntryTagSample/Tags.pm
sub _hdlr_entry_test_text1 {
  my ( $ctx, $args, $cond ) = @_;
  my $entry = $ctx->stash( 'entry' )
    or return $ctx->_no_entry_error( $ctx->stash( 'tag' ) );
  return $entry->test_text1;
}
```

# ブロックタグ

## 概要

ブロックタグでもハンドラの処理の戻り値がそのまま出力されますが、自分でビルドしながら戻り値の組み立てを行う必要があります。

1. あらかじめ処理対象のオブジェクトを配列に入れておく
2. 配列をループ処理しながら、一回ずつビルドを行い、その結果を連結していく
3. ループ処理が完了したら、できあがった結果を返す

## 例

下記の例では、記事の test_text1 カラムの値が「bar」である記事をループ処理する「MTEntriesTestText1IsBar」タグを作成することを想定しています。

### config.yaml の指定

```
# plugins/EntryTagSample/config.yaml
tags:
  block:
    EntriesTestText1IsBar: $entrytagsample::EntryTagSample::Tags::_hdlr_entries_test_text1_is_bar
```

### ハンドラの指定

```
# plugins/EntryTagSample/lib/EntryTagSample/Tags.pm
sub _hdlr_entries_test_text1_is_bar {
  my ( $ctx, $args, $cond ) = @_;
  # 対象の記事を load する(「MTオブジェクトの操作」を参照)
  my @entries = MT->model( 'entry' )->load(
    {
      test_text1 => 'bar',
    },
  );
  # ビルドに必要なオブジェクトの取得
  my $tokens = $ctx->stash( 'tokens' );
  my $builder = $ctx->stash( 'builder' );
  # ループ内で使用する変数の初期化
  my $res = '';
  my $i = 0;
  # 対象をループしながら一回ずつビルドし、その結果を連結していく
  for my $entry ( @entries ) {
    # ビルドの準備
    ## コンテキストをセットすることで、記事やワークスペース/スペースに関するテンプレートタグが動作する
    local $ctx->{ __stash }{ entry } = $entry;
    local $ctx->{ __stash }{ blog } = $entry->blog;
    local $ctx->{ __stash }{ blog_id } = $entry->blog_id;
    ## ブロックタグ内でテンプレート変数 __first__, __last__ などが使えるようになる
    local $ctx->{ __stash }->{ vars }->{ __first__ } = 1 if ( $i == 0 );
    local $ctx->{ __stash }->{ vars }->{ __counter__ } = $i + 1;
    local $ctx->{ __stash }->{ vars }->{ __odd__ } = ( $i % 2 ) == 0;
    local $ctx->{ __stash }->{ vars }->{ __even__ } = ( $i % 2 ) == 1;
    local $ctx->{ __stash }->{ vars }->{ __last__ } = 1 if ( !defined( $entries[ $i + 1 ] ) );
    ## アーカイブテンプレート等で使うものをセットする
    local $ctx->{ current_timestamp } = $entry->authored_on;
    local $ctx->{ modification_timestamp } = $entry->modified_on;
    ## 準備ができたらビルドし、今回のループでのビルド結果を得る
    my $out = $builder->build( $ctx, $tokens, $cond );
    if ( ! defined( $out ) ) { return $ctx->error( $builder->errstr ) };
    ## 今回のループでのビルド結果を連結する
    $res .= $out;
    $i++;
  }
  # ビルドされた結果を返す
  return $res;
}
```

# モディファイア

## 例

下記の例では、文字列「foo」を「bar」に置換する foo2bar モディファイアを作成することを想定しています。

### config.yaml の指定

```
# plugins/EntryTagSample/config.yaml
tags:
  modifier:
    foo2bar: $entrytagsample::EntryTagSample::Tags::_fltr_foo2bar
```

### ハンドラの指定

```
# plugins/EntryTagSample/lib/EntryTagSample/Tags.pm
sub _fltr_foo2bar {
  my ( $text, $arg, $ctx ) = @_; # ファンクションタグ / ブロックタグの場合とは異なる
  $text =~ s/foo/bar/g;
  return $text;
}
```
